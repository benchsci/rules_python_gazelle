load("@bazel_gazelle//:def.bzl", "gazelle_binary")
load("@io_bazel_rules_go//go:def.bzl", "go_library", "go_test")
load("@rules_python//python:defs.bzl", "py_binary")

go_library(
    name = "gazelle",
    srcs = [
        "configure.go",
        "fix.go",
        "generate.go",
        "kinds.go",
        "language.go",
        "lifecycle.go",
        "parser.go",
        "resolve.go",
        "std_modules.go",
        "target.go",
    ],
    embedsrcs = [":helper.zip"],  # keep
    importpath = "github.com/benchsci/rules_python_gazelle/gazelle",
    visibility = ["//visibility:public"],
    deps = [
        "//gazelle/manifest",
        "//gazelle/pythonconfig",
        "@bazel_gazelle//config:go_default_library",
        "@bazel_gazelle//label:go_default_library",
        "@bazel_gazelle//language:go_default_library",
        "@bazel_gazelle//repo:go_default_library",
        "@bazel_gazelle//resolve:go_default_library",
        "@bazel_gazelle//rule:go_default_library",
        "@com_github_bazelbuild_buildtools//build:go_default_library",
        "@com_github_emirpasic_gods//sets/treeset:go_default_library",
        "@com_github_emirpasic_gods//utils:go_default_library",
        "@com_github_google_uuid//:go_default_library",
        "@io_bazel_rules_go//go/tools/bazel:go_default_library",
    ],
)

py_binary(
    name = "parse",
    srcs = ["parse.py"],
    main = "parse.py",
    visibility = ["//visibility:public"],
)

py_binary(
    name = "std_modules",
    srcs = ["std_modules.py"],
    main = "std_modules.py",
    visibility = ["//visibility:public"],
)

py_binary(
    name = "helper",
    srcs = [
        "__main__.py",
        "parse.py",
        "std_modules.py",
    ],
    # This is to make sure that the current directory is added to PYTHONPATH
    imports = ["."],
    main = "__main__.py",
    visibility = ["//visibility:public"],
)

filegroup(
    name = "helper.zip",
    srcs = [":helper"],
    output_group = "python_zip_file",
)

TEST_DEPS = [
    "@bazel_gazelle//testtools:go_default_library",
    "@com_github_emirpasic_gods//lists/singlylinkedlist",
    "@com_github_ghodss_yaml//:yaml",
    "@io_bazel_rules_go//go/tools/bazel:go_default_library",
]

TEST_DATA = [
    ":gazelle_python_binary",
    ":parse",
    ":std_modules",
]

[
    go_test(
        name = "test_{}".format(t),
        srcs = ["python_test.go"],
        data = TEST_DATA + glob(["testdata/{}/**".format(t)]),
        deps = TEST_DEPS,
    )
    for t in [
        "simple",
        "django_test",
        "pycheck",
        "py_module",
    ]
]

gazelle_binary(
    name = "gazelle_python_binary",
    languages = ["//gazelle"],
    visibility = ["//visibility:public"],
)
